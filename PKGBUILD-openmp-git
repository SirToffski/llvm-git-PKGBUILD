# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=openmp-git
pkgver=18.0.0_r484735.9fba1d5f3a52
pkgrel=1
pkgdesc="LLVM OpenMP Runtime Library"
arch=('x86_64')
url="https://openmp.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm-libs-git' 'libelf' 'libffi')
makedepends=('llvm-git' 'llvm-libs-git' 'libclc-git' 'python' 'ninja' 'cmake' 'git')
optdepends=('cuda: offloading to NVIDIA GPUs'
            'hsa-rocr: offloading to AMD GPUs')
options=('!lto') # https://bugzilla.redhat.com/show_bug.cgi?id=1988155
provides=('openmp')
conflicts=('openmp')
source=('llvm-project::git+https://github.com/llvm/llvm-project.git#commit=9fba1d5f3a52af0ae62f386d0c494bd9510fa845')
md5sums=(SKIP)

pkgver() {
  cd llvm-project/llvm

   # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
          'BEGIN { ORS="." ; i=0 } \
           /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
           END { print "\n" }' \
           CMakeLists.txt)_r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

prepare() {
  cd llvm-project

  #patch -Np1 -i ${srcdir}/openmp.patch
  rm -rf openmp/build && mkdir openmp/build
}

build() {
  cd llvm-project/openmp/build

  export CXXFLAGS="${CXXFLAGS} -fno-lto"
  export CFLAGS="${CFLAGS} -fno-lto"

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBOMP_INSTALL_ALIASES=OFF
  ninja all


}

package() {
  cd "$srcdir/llvm-project/openmp/build"
  
  DESTDIR="${pkgdir}" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm "$pkgdir/usr/lib/libarcher_static.a"
}
